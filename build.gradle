buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven/" }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}
apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "scala"
apply plugin: "idea"

ext.buildProps = file "build.properties"

buildProps.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.version.minecraft}-${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"//." + getBuildNumber()
group = "extracells"
archivesBaseName = "ExtraCells"
def actualVersion = version

/*task outputVersionToFile {
	def versionFile = new File("version.txt")
	versionFile.write("${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}")
	def oldVersion = new File("versionOld.txt")
	if(!oldVersion.exists()) {
		oldVersion.write("0.0.0")
	}
}*/

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

/*logger.lifecycle "$archivesBaseName-$version"
logger.lifecycle "" + version*/

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}"
    runDir = "run"
	mappings = "${config.version.mappings}"
    replace "@VERSION@", "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"
    replaceIn "Constants.java"
}

repositories {
	maven {
        name = "Hwyla"
        url "https://maven.tehnut.info/"
    }
    maven {
        name = "CofhAndP455w0rd"
        url "https://maven.covers1624.net/"
    }
    maven {
        name = "Jei"
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "MCMultiPart"
        url "https://maven.amadornes.com/"
    }
    maven {
        name = "BuildCraft"
        url "https://mod-buildcraft.com/maven/"
    }
    maven {
        name = "IGW"
        url "https://maven.k-4u.nl/"
    }
    maven {
        name = "Curse"
        url "https://www.cursemaven.com/"
    }
    maven {
        name = "OpenComputers"
        url "https://maven.cil.li/"
    }
}

dependencies {
	deobfCompile "cofh:RedstoneFlux:${config.version.rf}:universal"
    deobfCompile "p455w0rd:AE2WTLib:${config.version.ae2wtlib}"
    deobfCompile "p455w0rd:p455w0rdslib:${config.version.p455w0rdslib}"
    deobfCompile "p455w0rd:WirelessCraftingTerminal:${config.version.wct}"
    deobfCompile "MCMultiPart2:MCMultiPart:${config.version.mcmp}"
    deobfCompile "li.cil.oc:OpenComputers:${config.version.oc}"
    deobfCompile "igwmod:IGW-Mod-${config.version.minecraft}:${config.version.igw}:universal"
    deobfCompile "curse.maven:baubles-227083:${config.version.baubles}"
    deobfCompile "curse.maven:mekanism-268560:${config.version.mekanism}"

    deobfProvided "com.mod-buildcraft:buildcraft-api:${config.version.bc}"
    runtime "com.mod-buildcraft:buildcraft-all:${config.version.bc}"

    deobfProvided "mezz.jei:jei_${config.version.minecraft}:${config.version.jei}:api"
    runtime "mezz.jei:jei_${config.version.minecraft}:${config.version.jei}"

    compileOnly ("cofh:CoFHCore:${config.version.cofhcore}:deobf") {
        transitive = false
    }
    deobfCompile ("mcp.mobius.waila:Hwyla:${config.version.hwyla}") {
        transitive = false
    }
    deobfCompile ("curse.maven:applied-energistics-2-223794:${config.version.ae2}"){
        transitive = false
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.lang'
        include '**/*.info'
        include '**/*.properties'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.lang'
        include '**/*.info'
        include '**/*.properties'
        expand 'version': "${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}", 'mcversion': project.minecraft.version
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task apiJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.scala
    from sourceSets.main.java
    classifier = 'api'
	include 'extracells/api/**'
}

if (System.getenv("CURSEFORGE_API_KEY") != null) {
	curseforge {
		apiKey = System.getenv("CURSEFORGE_API_KEY")
		project {
			id = config.cf.project.id
			addGameVersion config.version.minecraft
			changelog = file('changelog.txt')
			releaseType = config.cf.project.type
			relations {
				requiredLibrary 'applied-energistics-2'
			}
			addArtifact(apiJar) {
				releaseType = 'alpha'
			}
			mainArtifact(jar) {
				displayName = "$archivesBaseName-${config.version.minecraft}-${config.version.mod.major}.${config.version.mod.minor}.${config.version.mod.revis}"
			}
		}
	}
}

artifacts {
    archives apiJar
}

/*def getBuildNumber() {
    if (System.getenv("BUILD_NUMBER") != null) {
        return "${System.getenv("BUILD_NUMBER")}"
    }
    else if (System.getenv("DRONE_BUILD_NUMBER") != null) {
        return "${System.getenv("DRONE_BUILD_NUMBER")}"
    }
	else if (System.getenv("APPVEYOR_BUILD_NUMBER") != null) {
        return "${System.getenv("APPVEYOR_BUILD_NUMBER")}"
    }
    return "NIGHTLY"
}*/

idea {
    module {
		outputDir = file('build/classes/main')
    }
}
